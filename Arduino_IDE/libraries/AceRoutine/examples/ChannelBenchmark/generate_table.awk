#!/usr/bin/gawk -f
#
# Usage: generate_table.awk < ${board}.txt
#
# Takes the *.txt file generated by AutoBenchmark.ino and generates an ASCII
# table that can be inserted into the README.md. Collects both sizeof()
# information as well as CPU benchmarks.

BEGIN {
  # Set to 1 when 'BENCHMARKS' is detected
  collect_benchmarks = 0
}

/^BENCHMARKS/ {
  collect_benchmarks = 1
  benchmark_index = 0
  next
}

!/^END/ {
  if (collect_benchmarks) {
    u[benchmark_index]["name"] = $1
    u[benchmark_index]["micros"] = $2
    u[benchmark_index]["count"] = $3
    u[benchmark_index]["writeCount"] = $4
    u[benchmark_index]["readCount"] = $5
    benchmark_index++
  }
}

END {
  TOTAL_BENCHMARKS = benchmark_index

  # Calculate the diff from baseline
  baseline = u[0]["micros"]
  for (i = 0; i < TOTAL_BENCHMARKS; i++) {
    u[i]["diff"] = u[i]["micros"] - baseline
  }

  print ""
  print "CPU:"

  printf("+---------------+--------+-------------+--------+\n");
  printf("| Functionality |  count | micros/iter |   diff |\n");
  printf("+---------------+--------+-------------+--------+\n");
  for (i = 0; i < TOTAL_BENCHMARKS; i++) {
    name = u[i]["name"]

    printf("| %-13s | %6d | %11.3f | %6.3f |\n",
      name, u[i]["count"], u[i]["micros"], u[i]["diff"])
  }
  printf("+---------------+--------+-------------+--------+\n");
}
